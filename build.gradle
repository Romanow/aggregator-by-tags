plugins {
    id "idea"
    id "jacoco"
    id "maven-publish"
    id "org.jreleaser" version "1.19.0"
    id "org.jetbrains.kotlin.jvm" version "1.9.25"
    id "org.jlleitschuh.gradle.ktlint" version "12.2.0"
}

ext {
    kotlinVersion = "1.9.25"
    openApiVersion = "2.5.0"
    junitVersion = "5.11.0"
    assertjVersion = "3.27.3"
    slf4jVersion = "2.0.17"
    logbackVersion = "1.5.8"
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

[compileKotlin, compileTestKotlin]*.kotlinOptions*.jvmTarget = "17"

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    api "org.springdoc:springdoc-openapi-starter-webmvc-api:$openApiVersion"
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    api "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    api "org.slf4j:slf4j-api:$slf4jVersion"

    testImplementation "ch.qos.logback:logback-classic:$logbackVersion"
    testImplementation "org.assertj:assertj-core:$assertjVersion"
    testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

ktlint {
    version.set("1.1.1")
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = false
    }
}

check.dependsOn jacocoTestReport

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                name = "aggregator-by-tags"
                description = "OpenAPI aggregator by Tags"
                url = "https://github.com/Romanow/aggregator-by-tags/"
                licenses {
                    license {
                        name = "The MIT License"
                        url = "https://opensource.org/license/mit"
                    }
                }
                developers {
                    developer {
                        id = "Romanow"
                        name = "Romanov Alexey"
                        email = "romanow.tech@gmail.com"
                    }
                }
                scm {
                    url = "https://github.com/Romanow/aggregator-by-tags/"
                    connection = "scm:git:https://github.com/Romanow/aggregator-by-tags.git"
                    developerConnection = "scm:git:ssh://github.com/Romanow/aggregator-by-tags.git"
                }
            }
        }
    }
    repositories {
        maven {
            url = "build/staging-deploy"
        }
    }
}

jreleaser {
    signing {
        active = "ALWAYS"
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = "ALWAYS"
                    url = "https://central.sonatype.com/api/v1/publisher"
                    stagingRepository("build/staging-deploy")
                }
            }
        }
    }
}
